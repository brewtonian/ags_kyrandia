
// label to use for text actions
Label* action;

void set_ActionLabel(this TwoClickHandler*,  Label* label)
{
  action = label;
  action.Text = "";
}

Label* get_ActionLabel(this TwoClickHandler*)
{
  return action;
}

// GUI to use as inventory
GUI* interface_inv;

void set_InventoryGUI(this TwoClickHandler*,  GUI* invGui)
{
  interface_inv = invGui;
}

GUI* get_InventoryGUI(this TwoClickHandler*)
{
  return interface_inv;
}

// reversed control mode (left click to look)
bool classic = false;

void set_ReversedClicks(this TwoClickHandler*, bool reversed)
{
  classic = reversed;
}

bool get_ReversedClicks(this TwoClickHandler*)
{
  return classic;
}

MouseButton check_reversed(MouseButton button)
{
  if (classic)
  {
    if (button == eMouseLeft) return eMouseRight;
    if (button == eMouseRight) return eMouseLeft;
    if (button == eMouseLeftInv) return eMouseRightInv;
    if (button == eMouseRightInv) return eMouseLeftInv;
  }

  return button;
}

function do_room_action(MouseButton button)
{
  // Fire room exit arrows as Walkto
  if (Mouse.Mode == eModeUp || 
      Mouse.Mode == eModeRight ||
      Mouse.Mode == eModeDown || 
      Mouse.Mode == eModeLeft) Room.ProcessClick(mouse.x, mouse.y, eModeWalkto);
  
  
  // Play a noise if hotspot is set to No Exit
  else if (Mouse.Mode == eModeNoExit) aTick.Play();
  else 
  {
    if (GetLocationType(mouse.x, mouse.y) != eLocationNothing)
    {
      // clicked on something
      // and holding nothing
      if (player.ActiveInventory == null)
      {
        if (button == check_reversed(eMouseLeft))
        {
          // left click to interact with target
          Room.ProcessClick(mouse.x, mouse.y, eModeInteract);
        }
        else
        {
          // right click to look at target
          Room.ProcessClick(mouse.x, mouse.y, eModeLookat);
        }
      }
      // clicked on something
      // and holding an item
      else
      {
        if (button == check_reversed(eMouseLeft))
        {
          // left click to use inventory on target
          Room.ProcessClick(mouse.x, mouse.y, eModeUseinv);
        }
        else
        {
          drop_held_item();   // drop it!
        }
      }
    }
    else
    {
      // click on nothing
      // and holding nothing
      if (player.ActiveInventory == null)
      {
        // left click to walk
        Room.ProcessClick(mouse.x, mouse.y, eModeWalkto);
      }
      else
      // click on nothing
      // and holding something!
      {
        drop_held_item();   // drop it!
      }
    }
  }
}


function do_inventory_action(MouseButton button, InventoryItem* item)
{ 
  if (button == check_reversed(eMouseLeftInv))
  {
    if (player.ActiveInventory == null)  // Player is not holding anything
      take_item_from_slot(item);
    else
    {
      if (item.ID != player.ActiveInventory.ID)          //  Player is holding an item (slot 10)
      {   
        if (item.ID <= 10)  
          drop_item_in_slot(item);    // we're putting something into a blank slot
        else 
          switch_item_in_slot(item);  // we're switching inventory slots
      }
    }
  }
  else
  {

    if (player.ActiveInventory == null)    // is the player holding an object?
      item.RunInteraction(eModeLookat);    // No. Right click to look at inventory item    
    else
    { // Do nothing.
    }
  }
}

//----------------------------------------------------------------------------------------------------
// on_mouse_click()
//----------------------------------------------------------------------------------------------------
function on_mouse_click(MouseButton button)
{
  // when mouse is clicked, text label is cleared
  if (action != null)
  {
    action.Text = "";
  }

  if (!IsGamePaused() && (button == eMouseLeft || button == eMouseRight))
  {
    do_room_action(button);
  }
  else if (button == eMouseLeftInv || button == eMouseRightInv)
  {
    // InventoryItem.GetAtScreenXY could return null here
    // so using game.inv_activated instead is a safer option
    do_inventory_action(button, inventory[game.inv_activated]);
  }
}

//----------------------------------------------------------------------------------------------------
// repeatedly_execute()
//----------------------------------------------------------------------------------------------------
function repeatedly_execute()
{
  // Action Text
  // We always display the name of what is under the mouse, with one exception:
  // IF the player has an inventory item selected and hovers over the same inventory item,
  // we display nothing to indicate that an item can not be used on itself
  if (player.ActiveInventory == null)
  {
    if (action != null && !IsGamePaused())
    {
      action.Text = Game.GetLocationName(mouse.x, mouse.y);
    }
  }
  else
  {
    InventoryItem *item = InventoryItem.GetAtScreenXY(mouse.x, mouse.y);
    if (action == null)
    {
    }
    else if (item != null && item.ID == player.ActiveInventory.ID)
    {
      action.Text = "";
    }
    else
    {
      action.Text = Game.GetLocationName(mouse.x, mouse.y);
    }
  }      
}

// handle clicks in the inventory area that are not on an inventory item
function on_event(EventType event, int data)
{
  if (event == eEventGUIMouseDown &&
    interface_inv != null &&
    data == interface_inv.ID &&
    InventoryItem.GetAtScreenXY(mouse.x, mouse.y) == null)
  {
    GUIControl* control = GUIControl.GetAtScreenXY(mouse.x, mouse.y);

    if (control == null || control.AsInvWindow == null)
    {
      // pass
    }
    else if (player.ActiveInventory != null)
    {
      player.ActiveInventory = null;
    }
  }
}
